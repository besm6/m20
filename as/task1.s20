; Пример 1 из http://sergev.org/besm/m20
; Решение уравнения 1/32 x^5 - 3/4 x^2 - x + 4.005 = 0

х0	.вещ	2			; Константы
с4	.вещ	4.005
с1_32	.вещ	0.03125
эпсилон	.вещ	0.0001
с3_4	.вещ	0.75

раб	.перем	3			; Рабочие ячейки

начало:					; Команды
	п	х0, , раб		; Засылка x[0]
цикл:
	у	раб, раб, раб+1		; x[i]^2
	у	раб+1, раб, раб+2 	; x[i]^3
	у	раб+2, раб+1, раб+2 	; x[i]^5
	у	с1_32, раб+2, раб+2	; 1/32 x[i]^5
	у	раб+1, с3_4, раб+1 	; 3/4 x[i]^2
	в	раб+2, раб+1, раб+1 	; 1/32 x[i]^5 - 3/4 x[i]^2
	с	с4, раб+1, раб+1 	; x[i+1] = 1/32 x[i]^5 - 3/4 x[i]^2 + 4.005
	в	раб+1, раб, раб+2	; x[i+1] - x[i]
	ва	раб+2, эпсилон		; | x[i+1] - x[i] | - | ε |
	пу	раб+1, цикл, раб 	; Пересылка x[i+1] в 4000

	ма	02100, , раб		; Печать результата
	мб	раб

	стоп				; Остановка
